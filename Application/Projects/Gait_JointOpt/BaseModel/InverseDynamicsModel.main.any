#ifndef IncludeRight 
#define IncludeRight 1
#endif

#ifndef IncludeLeft 
#define IncludeLeft 1
#endif

#ifndef IncludeHip 
#define IncludeHip 1
#endif

#ifndef IncludeKnee 
#define IncludeKnee 1
#endif

#ifndef IncludeAnkle 
#define IncludeAnkle 1
#endif

#ifndef UseDanamicLoadPosition 
#define UseDanamicLoadPosition 1
#endif

#ifndef EnableJointAxisScaling 
#define EnableJointAxisScaling 1
#endif

#ifndef HideStickModelGraphics 
#define HideStickModelGraphics 0
#endif

#ifndef UseRbfScaling
#define UseRbfScaling 0
#endif

Main = {
//  AnyProject Project = { Files.MainFile = MAIN_FILE_NAME; };   
  
  #include "ModelSetup.any"
  #include "DrawSettings.any"
  #include "GeneralSettings.any"  
  
  AnyFolder JntParameterOptModel = {
    
    #include "KinOptModel\StickFigure.any"
    
    // The actual body model goes in this folder
    AnyFolder LegModel = {
      
      AnyFolder Trunk = {
        #include "KinOptModel\TrunkSegments.any"
      };
      
      #if IncludeRight == 1
      #define RightLeg
      AnyFolder Right = {
        #include "KinOptModel\LegSegments.any"
        #include "KinOptModel\Joints.any"
        AnyFloat Sign = 1;
      };   
      #undef RightLeg
      #endif
      
      #if IncludeLeft == 1
      #define LeftLeg
      AnyFolder Left = {
        #include "KinOptModel\LegSegments.any"
        #include "KinOptModel\Joints.any"
        AnyFloat Sign = -1;
      };   
      #undef LeftLeg
      #endif       
      
      #include "KinOptModel\Interface.any"
      
    };
    
    AnyFolder Environment = {
      #include "KinOptModel\Drivers.any"
    };
    
    
    AnyBodyStudy KinematicStudy = 
    {
      tStart = Main.ModelSetup.tStart;
      tEnd = Main.ModelSetup.tEnd;
      nStep = Main.ModelSetup.nStep;
      Gravity = {0, 0, 0};
      
      AnyFolder &C3DData=Main.ModelSetup.DynamicDataSet ;
      AnyFolder &LegModel = .LegModel;
      AnyFolder &Environment = .Environment;
      
      #include "KinOptModel\JointAngleOutputs.any"
      
      InitialConditions.SolverType = KinSolOverDeterminate;
      Kinematics.SolverType = KinSolOverDeterminate;   
      InitialConditions.PosAnalysisOnlyOnOff=On;
      Kinematics.PosAnalysisOnlyOnOff=On;
      
    };
    
  };
  
  #if IncludeLegTDModel  == 1
  AnyFolder AnyBodyGaitAppModel = {
    #include "LegTDModel\HumanModel.any"
    //    #include "Calculations\CalculateSourcePointProjections.any"    
    
    AnyBodyStudy InverseDynamicStudy = 
    {
      tStart = Main.ModelSetup.tStart; 
      tEnd = Main.ModelSetup.tEnd;
      nStep = Main.ModelSetup.nStep;
      Gravity = Main.GeneralSettings.Gravity;
      
      
      AnyFolder &OptKinModel = Main.JntParameterOptModel.LegModel;
      AnyFolder HumanModel = {
        AnyFolder &BodyModel = ..HumanModel.BodyModel ;
        AnyFolder &Scaling = ..HumanModel.Scaling;
        AnyFolder &Mannequin = ..HumanModel.Mannequin;
      }; 
      AnyFolder EnvironmentModel = {
        AnyFolder DrawC3DMarkers = {}; 
        #include "Environment.any"         
       };
      
      //Connection between environment and the human    
      AnyFolder ModelEnvironmentConnection = {
        //Drivers for the model
        #include "LegTDModel\JointsAndDriversOptimized.any"
        #include "KinOptModel\JointsAndDriversOptimized.any"
      };
      
      InverseDynamics.Criterion.UpperBoundOnOff=Off;
      InitialConditions.SolverType = KinSolOff;
      Kinematics.SolverType = KinSolOff;   
      InitialConditions.PosAnalysisOnlyOnOff=Off;
      Kinematics.PosAnalysisOnlyOnOff=Off;
      InverseDynamics.Criterion.Power = Main.GeneralSettings.MuscleRecruitmentOrder;
      
      
      #include "JointReactionForces.any"
      
      AnyOutputFile OutputFile = {
       FileName = Main.GeneralSettings.OutputFilename;
       #include "OutputDefinition.any"
       SepSign = Main.GeneralSettings.OutputSepSign;
      };
     
   }; 
    
    
    
 };
  
  #endif
  
};  // Main


