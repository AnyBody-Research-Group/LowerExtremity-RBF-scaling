
#class_template CreateForcePlateGrandChallenge (AnySeg &Limb, Folder,No, Fx, Fy, Fz, COPx, COPy, COPz, Mz) {
  
  
  AnyFixedRefFrame Corners ={
    AnyVar CoordinateSystemSize= 0.1;
    
    AnyRefNode c01={
      AnyInt i=0;
      AnyInt plnr=No;
      sRel=0.001*{Folder.Groups.FORCE_PLATFORM.CORNERS.Data[plnr][i][0],Folder.Groups.FORCE_PLATFORM.CORNERS.Data[No][i][1],Folder.Groups.FORCE_PLATFORM.CORNERS.Data[No][i][2]};
      AnyDrawNode drw={ScaleXYZ=0.01*{1,1,1};RGB={1,0,0};};
      AnyDrawVector  DrawName = {
        Vec = {0.0,0,0};  //use zero length
        Line.Thickness = 0.025; //arbitary value
        Text = "1"; //make reference to name
        Line.RGB={1,0,0};//make reference to color
      };
    };
    
    AnyRefNode c02={
      AnyInt i=1;
      AnyInt plnr=No;
      sRel=0.001*{Folder.Groups.FORCE_PLATFORM.CORNERS.Data[plnr][i][0],Folder.Groups.FORCE_PLATFORM.CORNERS.Data[No][i][1],Folder.Groups.FORCE_PLATFORM.CORNERS.Data[No][i][2]};
      AnyDrawNode drw={ScaleXYZ=0.01*{1,1,1};RGB={1,0,0};};
      AnyDrawVector  DrawName = {
        Vec = {0.0,0,0};  //use zero length
        Line.Thickness = 0.025; //arbitary value
        Text = "2"; //make reference to name
        Line.RGB={1,0,0};//make reference to color
      };
    };
    
    AnyRefNode c03={
      AnyInt i=2;
      AnyInt plnr=No;
      sRel=0.001*{Folder.Groups.FORCE_PLATFORM.CORNERS.Data[plnr][i][0],Folder.Groups.FORCE_PLATFORM.CORNERS.Data[No][i][1],Folder.Groups.FORCE_PLATFORM.CORNERS.Data[No][i][2]};
      AnyDrawNode drw={ScaleXYZ=0.01*{1,1,1};RGB={1,0,0};};
      
      AnyDrawVector  DrawName = {
        Vec = {0.0,0,0};  //use zero length
        Line.Thickness = 0.025; //arbitary value
        Text = "3"; //make reference to name
        Line.RGB={1,0,0};//make reference to color
      };
    };
    
    AnyRefNode c04={
      AnyInt i=3;
      AnyInt plnr=No;
      sRel=0.001*{Folder.Groups.FORCE_PLATFORM.CORNERS.Data[plnr][i][0],Folder.Groups.FORCE_PLATFORM.CORNERS.Data[No][i][1],Folder.Groups.FORCE_PLATFORM.CORNERS.Data[No][i][2]};
      AnyDrawNode drw={ScaleXYZ=0.01*{1,1,1};RGB={1,0,0};};
      AnyDrawVector  DrawName = {
        Vec = {0.0,0,0};  //use zero length
        Line.Thickness = 0.025; //arbitary value
        Text = "4"; //make reference to name
        Line.RGB={1,0,0};//make reference to color
      };
    };
    
    AnyRefNode PlateCenter={
      sRel=0.25*(.c01.sRel+.c02.sRel+.c03.sRel+.c04.sRel);
      AnyVec3 p1=sRel;
      AnyVec3 p2=0.5*(.c01.sRel+.c04.sRel);
      AnyVec3 p3=0.5*(.c01.sRel+.c02.sRel);
      ARel =RotMat(p1,p2,p3);
      //      AnyDrawRefFrame drw={ScaleXYZ=..CoordinateSystemSize*{1,1,1};RGB={0,1,0};};
      
      AnyVec3 Size={vnorm(..Corners.c01.sRel-..Corners.c02.sRel,2),vnorm(..Corners.c02.sRel-..Corners.c03.sRel,2),0.1};
      
      AnyRefNode PlateGraphics ={
        sRel={0,0,-0.05};
        
        AnyVec3 Size={vnorm(..c01.sRel-..c02.sRel,2),vnorm(..c02.sRel-..c03.sRel,2),-0.1};
        
        AnyDrawSurf DrwBox = {
          FileName = "box";
          RGB = 0.45*{1,1,1};
          ScaleXYZ=.Size/0.30;
          Opacity =0.4;
          Face=-1;
        };
      };
    
    };
  
  
  }; 


  AnyInt   EndFrame =Folder.Header.FirstFrameNo-1+(Folder.Header.LastFrameNo-Folder.Header.FirstFrameNo+1)* Folder.Header.NoAnalogSamplesPer3DFrame;
  AnyFloatVar  tStart = Folder.Header.FirstFrameNo/Folder.Header.VideoFrameRate;
  AnyFloatVar   tEnd =  Folder.Header.LastFrameNo/Folder.Header.VideoFrameRate;
  
  AnyInt AnalogData=EndFrame-Folder.Header.FirstFrameNo;
  AnyFloat Time=tStart+(iarr(0,AnalogData)*(tEnd-tStart)/AnalogData);

  
  AnyFixedRefFrame GlobalRef ={
    Origin={0,0,0}; 
  };
  
  AnySeg CenterOfPressure={
    Mass=0;
    Jii={0.0,0.0,0.0};
    AnyRefNode node={
      sRel={0,0,0};
      AnyDrawNode drw={ScaleXYZ={0.015,.015,.005}; RGB={1,0,0};};
    };
  };
  
  AnyKinLinear Lin1={
    AnyRefFrame &ref1=.GlobalRef;
    AnyRefFrame &ref2=.CenterOfPressure;
  };
  
  AnyKinRotational Rot1={
    AnyRefFrame &ref1=.GlobalRef;
    AnyRefFrame &ref2=.CenterOfPressure;
    Type=RotVector;
  };
  
  //In other models the center of pressure is moved sideways a bit to get realistic adduction/abduction of the ankel
  AnyKinEqInterPolDriver LinDrv={
    AnyKinLinear &ref1= .Lin1;
    Type=PiecewiseLinear;
    T = .Time;
    Data = 1/1000*{ COPx, COPy, COPz};
    Reaction.Type={Off,Off,Off};
  };
  
  AnyKinEqSimpleDriver RotDrv={
    AnyKinRotational &ref1= .Rot1;
    DriverPos={0,0,0};
    DriverVel={0,0,0};
    Reaction.Type={Off,Off,Off};
  };
  
  AnyReacForce ForcePlateContactForce={
    
    AnyKinLinear Lin={
      //very very important this ensure that the reaction is between artifical segment and the foot
      //and not between foot and globalref if Ref had been equal -1 which is default
      Ref=0; 
      AnySeg &ref1=..CenterOfPressure;
      AnySeg &ref2= ..Limb;
    };
    
    AnyKinRotational Rot= {
      AnySeg &ref1=..CenterOfPressure;
      AnySeg &ref2= ..Limb;
      Type=RotVector;
    };
  };
  
  AnyVar FzTotal=ForceOnForcePlate.force(LinDrv.t)[2];
  AnyVar OnOff=iffun(gtfun(FzTotal,10.0),1.0,0.0);
  
  AnyForce3D ForceOnForcePlate ={
    
    AnyFunInterpol force ={
      Type=Bspline;
      BsplineOrder = 8;
      T = ..Time;
      Data = { Fx, Fy, Fz};
    }; 
    AnySeg &ref1=.CenterOfPressure;
    F = .OnOff*force(t);
    
  };
  
  
  AnyMoment3D MomentOnForcePlate ={
    
    AnyFunInterpol moment ={
      Type=Bspline;
      BsplineOrder = 8;
      T = ..Time;
      Data = 1/1000*{ 0*iarr(1,NumElemOf(Mz)) , 0*iarr(1,NumElemOf(Mz)) , Mz};
    }; 
    AnySeg &ref1=.CenterOfPressure;
    M = .OnOff*moment(t);
  };
  
  AnyDrawVector DrawSupport = {
    AnyRefFrame &ref = .CenterOfPressure;
    Vec = 0.0003*.ForceOnForcePlate.F;
    
    PointAway = Off;
    DrawCoord = Off;
    
    Line.RGB = {0,0,0};
    Line.Thickness =0.0075;
    Line.End.Thickness = 2*0.0075;
    Line.End.Length = 4*0.0075;
  };
  AnyDrawVector DrawSupport2 = {
    AnyRefFrame &ref = .CenterOfPressure;
    Vec = 0.0014*.ForceOnForcePlate.F;
    
    PointAway = On;
    DrawCoord = Off;
    
    Line.RGB = {0,0,1};
    Line.Thickness =0.0075;
    Line.End.Thickness = 2*0.0075;
    Line.End.Length = 4*0.0075;
  };

};
